buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'http://repo.enonic.com/public'
        }
    }

    dependencies {
        classpath "com.enonic.xp:gradle-plugin:${xpVersion}"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

apply plugin: 'com.enonic.xp.app'
apply plugin: 'com.jfrog.bintray'

app {
    name = project.appName
    displayName = project.displayName
    vendorName = 'Enonic AS'
    vendorUrl = 'http://enonic.com'
}

configurations {
    rcdjsfwkcore
    rcdjsfwkmaterial
}

dependencies {
    compile "com.enonic.xp:core-api:${xpVersion}"
    compile "com.enonic.xp:admin-api:${xpVersion}"
    compile "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-mustache:${xpVersion}"
    include "com.enonic.xp:lib-io:${xpVersion}"
    include "systems.rcd.fwk.core:rcd-fwk-core:7.0.0-SNAPSHOT"
    rcdjsfwkcore "systems.rcd.js.fwk.core:rcd-jsfwk-core:1.0.0-SNAPSHOT"
    rcdjsfwkmaterial "systems.rcd.js.fwk.material:rcd-jsfwk-material:1.0.0-SNAPSHOT"
}

repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'http://repo.enonic.com/public'
    }
}

task extractRcdJsFwk( type: Copy ) {
    from { configurations.rcdjsfwkcore.collect { zipTree( it ); } }
    from { configurations.rcdjsfwkmaterial.collect { zipTree( it ); } }
    into "${rootProject.buildDir}/resources/main/"
    exclude 'META-INF/**'
}
processResources.dependsOn extractRcdJsFwk

//TODO Replace by proper plugin
task lessc( dependsOn: processResources ) << {
    exec {
        executable 'lessc'
        args 'build/resources/main/assets/style/main.less',
             'build/resources/main/assets/style/main.css'
    }
}
jar.dependsOn lessc

bintray {
    user = project.hasProperty( 'user' ) ? project.property( 'user' ) : "user"
    key = project.hasProperty( 'key' ) ? project.property( 'key' ) : "key"
    configurations = ['archives']
    pkg {
        repo = 'rcd-repo'
        name = 'datatoolbox'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/GlennRicaud/data-toolbox-app.git'
    }
}